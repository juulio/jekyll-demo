<!doctype html>
<html lang="en">

<head>
    <meta charset="utf-8" />
    <title>Julio Del Valle - Costa Rica</title>
    <meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=no" />
    <meta name="HandheldFriendly" content="true">
    <meta name="description" content="Julio Del Valle threejs 3d javascript projects - Costa Rica 2021">
    <meta name="apple-mobile-web-app-capable" content="yes">
    <link rel="stylesheet" href="css/styles.css" media="screen" />
</head>

<body class="home">
    <header>
        <nav>
            <span>juliodelvalle.com</span>
        </nav>
        <ul class="projectList">
            <li>
                <span>three.js</span>
                <ul class="submenu">
                    <li>
                        <a id="scene01" href="#">cubes</a>
                    </li>
                    <li>
                        <a id="scene02" href="#">amusement park</a>
                    </li>
                    <li>
                        <a href="projects/volcanic-landscape">volcanic landscape</a>
                    </li>
                    <li>
                        <a href="projects/mosque">mosque</a>
                    </li>
                    <li>
                        <a href="projects/black-box">black-box</a>
                    </li>
                    <li>
                        <a href="projects/xmas">xmas</a>
                    <li>
                        <a href="projects/skybox">skybox</a>
                    </li>
                    <li>
                        <a href="projects/calendar">3D calendar</a>
                    </li>
                    <li>
                        <a href="projects/wireframe-box">wireframe box</a>
                    </li>
                    <li>
                        <a id="scene10" href="#">shader spheres</a>
                    </li>
                </ul>
            </li>
            <li>
                <span>p5.js</span>
                <ul class="submenu">
                    <li>
                        <a href="projects/p5.js">particle system</a>
                    </li>
                </ul>
            </li>
        </ul>
    </header>

    <script id="vertexShader" type="x-shader/x-vertex">
        uniform float delta;

        void main() {
            vec4 modelViewPosition = modelViewMatrix * vec4(position, 1.0);
            gl_Position = projectionMatrix * modelViewPosition;
       }
    </script>

    <script id="voronoiFragmentShader" type="x-shader/x-fragment">
        // Author: @patriciogv - 2015
        // Title: Tissue

        #ifdef GL_ES
        precision mediump float;
        #endif

        uniform vec2 u_resolution;
        uniform vec2 u_mouse;
        uniform float u_time;

        // Created by inigo quilez - iq/2013
        // License Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.
        // http://www.iquilezles.org/www/articles/voronoilines/voronoilines.htm

        vec2 random2( vec2 p ) {
            return fract(sin(vec2(dot(p,vec2(127.1,311.7)),dot(p,vec2(269.5,183.3))))*43758.5453);
        }

        #define ANIMATE
        vec3 voronoi( in vec2 x, float rnd ) {
            vec2 n = floor(x);
            vec2 f = fract(x);

            // first pass: regular voronoi
            vec2 mg, mr;
            float md = 8.0;
            for (int j=-1; j<=1; j++ ) {
                for (int i=-1; i<=1; i++ ) {
                    vec2 g = vec2(float(i),float(j));
                    vec2 o = random2( n + g )*rnd;
                    #ifdef ANIMATE
                    o = 0.5 + 0.5*sin( u_time + 6.2831*o );
                    #endif
                    vec2 r = g + o - f;
                    float d = dot(r,r);

                    if( d<md ) {
                        md = d;
                        mr = r;
                        mg = g;
                    }
                }
            }

            // second pass: distance to borders
            md = 8.0;
            for (int j=-2; j<=2; j++ ) {
                for (int i=-2; i<=2; i++ ) {
                    vec2 g = mg + vec2(float(i),float(j));
                    vec2 o = random2(n + g)*rnd;
                    #ifdef ANIMATE
                    o = 0.5 + 0.5*sin( u_time + 6.2831*o );
                    #endif
                    vec2 r = g + o - f;

                    if( dot(mr-r,mr-r)>0.00001 )
                    md = min( md, dot( 0.5*(mr+r), normalize(r-mr) ) );
                }
            }
            return vec3( md, mr );
        }

        void main() {
            vec2 st = gl_FragCoord.xy/u_resolution.xy;
            st = (st-.5)*.75+.5;
            if (u_resolution.y > u_resolution.x ) {
                st.y *= u_resolution.y/u_resolution.x;
                st.y -= (u_resolution.y*.5-u_resolution.x*.5)/u_resolution.x;
            } else {
                st.x *= u_resolution.x/u_resolution.y;
                st.x -= (u_resolution.x*.5-u_resolution.y*.5)/u_resolution.y;
            }
            vec3 color = vec3(0.0);

            float d = dot(st-.5,st-.5);
            vec3 c = voronoi( 20.*st, pow(d,.4) );

            // borders
            color = mix( vec3(1.0), color, smoothstep( 0.01, 0.02, c.x ) );
            // feature points
            float dd = length( c.yz );
            color += vec3(1.)*(1.0-smoothstep( 0.0, 0.1, dd));

            gl_FragColor = vec4(color,1.0);
        }
    </script>

    <script id="jaguarFragmentShader" type="x-shader/x-fragment">
        // Properly working jaguar code
        #ifdef GL_ES
        precision mediump float;
        #endif

        uniform vec2 u_resolution;
        uniform vec2 u_mouse;
        uniform float u_time;

        vec3 mod289(vec3 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
        vec2 mod289(vec2 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
        vec3 permute(vec3 x) { return mod289(((x*34.0)+1.0)*x); }

        float random (in vec2 st) {
            return fract(sin(dot(st.xy,
                                 vec2(12.9898,78.233)))*
                43758.5453123);
        }

        // Based on Morgan McGuire @morgan3d
        // https://www.shadertoy.com/view/4dS3Wd
        float noise (in vec2 st) {
            vec2 i = floor(st);
            vec2 f = fract(st);

            // Four corners in 2D of a tile
            float a = random(i);
            float b = random(i + vec2(1.0, 0.0));
            float c = random(i + vec2(0.0, 1.0));
            float d = random(i + vec2(1.0, 1.0));

            vec2 u = f * f * (3.0 - 2.0 * f);

            return mix(a, b, u.x) +
                    (c - a)* u.y * (1.0 - u.x) +
                    (d - b) * u.x * u.y;
        }

        float snoise(vec2 v) {
            const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                                0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                                -0.577350269189626,  // -1.0 + 2.0 * C.x
                                0.024390243902439); // 1.0 / 41.0
            vec2 i  = floor(v + dot(v, C.yy) );
            vec2 x0 = v -   i + dot(i, C.xx);
            vec2 i1;
            i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
            vec4 x12 = x0.xyxy + C.xxzz;
            x12.xy -= i1;
            i = mod289(i); // Avoid truncation effects in permutation
            vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
                + i.x + vec3(0.0, i1.x, 1.0 ));

            vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
            m = m*m ;
            m = m*m ;
            vec3 x = 2.0 * fract(p * C.www) - 1.0;
            vec3 h = abs(x) - 0.5;
            vec3 ox = floor(x + 0.5);
            vec3 a0 = x - ox;
            m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
            vec3 g;
            g.x  = a0.x  * x0.x  + h.x  * x0.y;
            g.yz = a0.yz * x12.xz + h.yz * x12.yw;
            return 130.0 * dot(m, g);
        }

        #define OCTAVES 6
        float fbm (in vec2 st) {
            // Initial values
            float value = 0.0;
            float amplitud = .5;
            float frequency = 0.;
            //
            // Loop of octaves
            for (int i = 0; i < OCTAVES; i++) {
                value += amplitud * noise(st);
                st *= 2.;
                amplitud *= .5;
            }
            return value;
        }

        float turbulence(in vec2 st) {
            float value = 0.0;
            float amplitude = 1.0;
            for (int i = 0; i < OCTAVES; i++) {
                value += amplitude * abs(snoise(st));
                st *= 2.;
                amplitude *= .5;
            }
            return value;
        }

        void main() {
            vec2 st = gl_FragCoord.xy/u_resolution.xy;
            st.x *= u_resolution.x/u_resolution.y;
            st.x += fbm(st) * 0.5;
            st.y += fbm(st + vec2(1.0)) * 0.2;
            vec3 color = vec3(0.980,0.995,0.965);
            color = mix(color, vec3(1.000,0.598,0.018), turbulence(st* 5.0));
            color = mix(color, vec3(0.003,0.060,0.135), turbulence(st* 12.0));
            gl_FragColor = vec4(color,1.0);
        }
    </script>

    <script id="redPulseFragmentShader" type="x-shader/x-fragment">
        uniform float u_time;

        void main(){
            gl_FragColor = vec4(abs(sin(u_time)),0.0,0.0,1.0);
        }
    </script>

    <script id="bwMatrixFragmentShader" type="x-shader/x-fragment">
        // Author @patriciogv - 2015
        // http://patriciogonzalezvivo.com

        #ifdef GL_ES
        precision mediump float;
        #endif

        uniform vec2 u_resolution;
        uniform vec2 u_mouse;
        uniform float u_time;

        float random(in float x){
            return fract(sin(x)*43758.5453);
        }

        float random(in vec2 st){
            return fract(sin(dot(st.xy ,vec2(12.9898,78.233))) * 43758.5453);
        }

        float rchar(in vec2 outer,in vec2 inner){
            float grid = 5.;
            vec2 margin = vec2(.2,.05);
            float seed = 23.;
            vec2 borders = step(margin,inner)*step(margin,1.-inner);
            return step(.5,random(outer*seed+floor(inner*grid))) * borders.x * borders.y;
        }

        vec3 matrix(in vec2 st){
            float rows = 80.0;
            vec2 ipos = floor(st*rows);

            ipos += vec2(.0,floor(u_time*20.*random(ipos.x)));


            vec2 fpos = fract(st*rows);
            vec2 center = (.5-fpos);

            float pct = random(ipos);
            float glow = (1.-dot(center,center)*3.)*2.0;

            // vec3 color = vec3(0.643,0.851,0.690) * ( rchar(ipos,fpos) * pct );
            // color +=  vec3(0.027,0.180,0.063) * pct * glow;
            return vec3(rchar(ipos,fpos) * pct * glow);
        }

        void main(){
            vec2 st = gl_FragCoord.st/u_resolution.xy;
            st.y *= u_resolution.y/u_resolution.x;
            vec3 color = vec3(0.0);

            color = matrix(st);
            gl_FragColor = vec4( 1.-color , 1.0);
        }
    </script>

    <script id="rotatedTilesFragmentShader" type="x-shader/x-fragment">
        // Author @patriciogv ( patriciogonzalezvivo.com ) - 2015

        #ifdef GL_ES
        precision mediump float;
        #endif

        #define PI 3.14159265358979323846
        #define TWO_PI 6.28318530717958647693

        uniform vec2 u_resolution;
        uniform float u_time;

        vec2 rotate2D (vec2 _st, float _angle) {
          _st -= 0.5;
          _st =  mat2(cos(_angle),-sin(_angle),
                      sin(_angle),cos(_angle)) * _st;
          _st += 0.5;
          return _st;
        }

        vec2 tile (vec2 _st, float _zoom) {
          _st *= _zoom;
          return fract(_st);
        }

        vec2 rotateTile(vec2 _st){
            _st *= 2.0;

            float index = 0.0;
            if (fract(_st.x * 0.5) > 0.5){
                index += 1.0;
            }
            if (fract(_st.y * 0.5) > 0.5){
                index += 2.0;
            }

            _st = fract(_st);

            if(index == 1.0){
                _st = rotate2D(_st,PI*0.5);
            } else if(index == 2.0){
                _st = rotate2D(_st,PI*-0.5);
            } else if(index == 3.0){
                _st = rotate2D(_st,PI);
            }

            return _st;
        }

        // Based on https://www.shadertoy.com/view/4sSSzG
        float triangle (vec2 _st,
                        vec2 _p0, vec2 _p1, vec2 _p2,
                        float _smoothness) {
          vec3 e0, e1, e2;

          e0.xy = normalize(_p1 - _p0).yx * vec2(+1.0, -1.0);
          e1.xy = normalize(_p2 - _p1).yx * vec2(+1.0, -1.0);
          e2.xy = normalize(_p0 - _p2).yx * vec2(+1.0, -1.0);

          e0.z = dot(e0.xy, _p0) - _smoothness;
          e1.z = dot(e1.xy, _p1) - _smoothness;
          e2.z = dot(e2.xy, _p2) - _smoothness;

          float a = max(0.0, dot(e0.xy, _st) - e0.z);
          float b = max(0.0, dot(e1.xy, _st) - e1.z);
          float c = max(0.0, dot(e2.xy, _st) - e2.z);

          return smoothstep(_smoothness * 2.0,
                            1e-7,
                            length(vec3(a, b, c)));
        }

        void main (void) {
            vec2 st = gl_FragCoord.xy/u_resolution.xy;

            st = tile(st,3.0);
            st = rotateTile(st);

            float pattern = 0.0;

            st = rotate2D(st,-PI*u_time*0.25);
            pattern =   triangle(st,
                                 vec2(0.30,-0.5),
                                 vec2(0.70,0.-0.5),
                                 vec2(0.5,1.0),
                                 0.01);

            vec3 color = vec3(pattern);

            gl_FragColor = vec4(color,1.0);
        }
    </script>

    <script id="noiseFragmentShader" type="x-shader/x-fragment">
        #ifdef GL_ES
        precision mediump float;
        #endif

        uniform vec2 u_resolution;
        uniform vec2 u_mouse;
        uniform float u_time;

        float random (in float x) {
            return fract(sin(x)*1e4);
        }

        // Based on Morgan McGuire @morgan3d
        // https://www.shadertoy.com/view/4dS3Wd
        float noise (in vec3 p) {
            const vec3 step = vec3(110.0, 241.0, 171.0);

            vec3 i = floor(p);
            vec3 f = fract(p);

            // For performance, compute the base input to a
            // 1D random from the integer part of the
            // argument and the incremental change to the
            // 1D based on the 3D -> 1D wrapping
            float n = dot(i, step);

            vec3 u = f * f * (3.0 - 2.0 * f);
            return mix( mix(mix(random(n + dot(step, vec3(0,0,0))),
                                random(n + dot(step, vec3(1,0,0))),
                                u.x),
                            mix(random(n + dot(step, vec3(0,1,0))),
                                random(n + dot(step, vec3(1,1,0))),
                                u.x),
                        u.y),
                        mix(mix(random(n + dot(step, vec3(0,0,1))),
                                random(n + dot(step, vec3(1,0,1))),
                                u.x),
                            mix(random(n + dot(step, vec3(0,1,1))),
                                random(n + dot(step, vec3(1,1,1))),
                                u.x),
                        u.y),
                    u.z);
        }

        void main() {
            vec2 st = gl_FragCoord.xy/u_resolution.xy;
            vec3 color = vec3(0.0);

            vec3 pos = vec3(st*5.0,u_time*0.5);

            color = vec3(noise(pos));

            gl_FragColor = vec4(color,1.0);
        }
    </script>

    <script id="simplexGridFragmentShader" type="x-shader/x-fragment">
        // Author @patriciogv - 2015 - patriciogonzalezvivo.com

        #ifdef GL_ES
        precision mediump float;
        #endif

        uniform vec2 u_resolution;
        uniform vec2 u_mouse;
        uniform float u_time;

        vec2 skew (vec2 st) {
            vec2 r = vec2(0.0);
            r.x = 1.1547*st.x;
            r.y = st.y+0.5*r.x;
            return r;
        }

        vec3 simplexGrid (vec2 st) {
            vec3 xyz = vec3(0.0);

            vec2 p = fract(skew(st));
            if (p.x > p.y) {
                xyz.xy = 1.0-vec2(p.x,p.y-p.x);
                xyz.z = p.y;
            } else {
                xyz.yz = 1.0-vec2(p.x-p.y,p.y);
                xyz.x = p.x;
            }

            return fract(xyz);
        }

        void main() {
            vec2 st = gl_FragCoord.xy/u_resolution.xy;
            vec3 color = vec3(0.0);

            // Scale the space to see the grid
            st *= 10.;

            // Show the 2D grid
            color.rg = fract(st);

            // Skew the 2D grid
            // color.rg = fract(skew(st));

            // Subdivide the grid into to equilateral triangles
            // color = simplexGrid(st);

            gl_FragColor = vec4(color,1.0);
        }
    </script>

    <script id="displacementFragmentShader" type="x-shader/x-fragment">
        // Author @kyndinfo - 2016
        // http://www.kynd.info
        // Title: Displacement

        #ifdef GL_ES
        precision mediump float;
        #endif

        uniform vec2 u_resolution;
        uniform vec2 u_mouse;
        uniform float u_time;

        float random (in vec2 st) {
            return fract(sin(dot(st.xy,
                                 vec2(12.9898,78.233)))*
                43758.5453123);
        }

        vec2 random2(vec2 p) {
            return fract(sin(vec2(dot(p,vec2(127.1,311.7)),dot(p,vec2(269.5,183.3))))*43758.5453);
        }

        float cellular(vec2 p) {
            vec2 i_st = floor(p);
            vec2 f_st = fract(p);
            float m_dist = 10.;
            for (int j=-1; j<=1; j++ ) {
                for (int i=-1; i<=1; i++ ) {
                    vec2 neighbor = vec2(float(i),float(j));
                    vec2 point = random2(i_st + neighbor);
                    point = 0.5 + 0.5*sin(6.2831*point);
                    vec2 diff = neighbor + point - f_st;
                    float dist = length(diff);
                    if( dist < m_dist ) {
                        m_dist = dist;
                    }
                }
            }
            return m_dist;
        }

        void main() {
            vec2 st = gl_FragCoord.xy / u_resolution.xy;
            st.x *= u_resolution.x / u_resolution.y;
            st *= 5.0;
            float r = cellular(st);
            float b = cellular(st - vec2(0.0, sin(u_time * 0.5) * 0.5));
            gl_FragColor = vec4(r, 0.0, b, 1.0);
        }
    </script>
    <script src="js/vendor/three.min.js"></script>
    <script type="module" src="js/scenes/scene10.js"></script>
    <script type="module" src="js/main.js"></script>
</body>

</html>
